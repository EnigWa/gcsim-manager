clorinde char lvl=90/90 cons=0 talent=9,9,9;
clorinde add weapon="finaleofthedeep" refine=5 lvl=90/90;
clorinde add set="fohw" count=4;
clorinde add stats hp=4780 atk=311 atk%=.466 electro%=.466 cd=.622;

fischl char lvl=90/90 cons=0 talent=9,9,9;
fischl add weapon="stringless" refine=3 lvl=90/90;
fischl add set="gt" count=4;
fischl add stats hp=4780 atk=311 em=187 electro%=.466 cr=.311;

nahida char lvl=90/90 cons=0 talent=9,9,9;
nahida add weapon="wanderingevenstar" refine=1 lvl=90/90;
nahida add set="totm" count=4;
nahida add stats hp=4780 atk=311 em=187 em=187 em=187;

lanyan char lvl=90/90 cons=0 talent=9,9,9;
lanyan add weapon="hakushinring" refine=5 lvl=90/90;
lanyan add set="vv" count=4;
lanyan add stats hp=4780 atk=311 atk%=.466 atk%=.466 atk%=.466;

target lvl=100 resist=0.1 radius=2 pos=0,2.4 hp=999999999; 
energy every interval=480,720 amount=1;

fn clorcombo() {
  clorinde skill;
  while .clorinde.status.clorinde-night-watch {
    switch {
      case .clorinde.bolratio >=1:
        clorinde skill;
      default:
        clorinde attack;
    }
  }
}

active fischl;

fischl burst;
lanyan skill:2;
nahida skill, attack;
clorcombo();

for let l=0; l<4; l=l+1 {
  lanyan attack:2;
  clorinde burst;
  nahida skill;
  if .fischl.burst.ready {
    fischl attack, burst;
  } else {
    fischl attack, skill;
  }
  lanyan skill:2;
  clorcombo();
}
